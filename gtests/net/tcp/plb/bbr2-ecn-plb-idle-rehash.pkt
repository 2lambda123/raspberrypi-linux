// Verify BBR v2 changes flowlabel in ipv6 header when there are
// net.ipv4.tcp_plb_idle_rehash_rounds=3 consecutive rounds with the ECN mark rate >=
// net.ipv4.tcp_plb_cong_thresh=64(25%) and there are no packets in flight.

`../common/defaults.sh
sysctl -q net.ipv4.tcp_ecn=1  # fully enabled
sysctl -q net.ipv4.tcp_congestion_control=bbr2
sysctl -q net.ipv4.tcp_plb_enabled=1
sysctl -q net.ipv4.tcp_plb_cong_thresh=64
sysctl -q net.ipv4.tcp_plb_idle_rehash_rounds=3
sysctl -q net.ipv4.tcp_plb_rehash_rounds=12
echo 1 > /sys/module/tcp_bbr2/parameters/ecn_enable
echo 0 > /sys/module/tcp_bbr2/parameters/ecn_max_rtt_us
`

// Initialize connection
    0 socket(..., SOCK_STREAM, IPPROTO_TCP) = 3
   +0 setsockopt(3, SOL_SOCKET, SO_REUSEADDR, [1], 4) = 0
   +0 bind(3, ..., ...) = 0
   +0 listen(3, 1) = 0

   +0 < SEW 0:0(0) ack 1 win 32792 <mss 1012,sackOK,TS val 200 ecr 0,nop,wscale 7>
   +0 > (flowlabel 0x1) SE. 0:0(0) ack 1 <mss 1460,sackOK,TS val 100 ecr 200,nop,wscale 8>
   +0 < . 1:1(0) ack 1 win 1000 <nop,nop,TS val 200 ecr 100>
   +0 accept(3, ..., ...) = 4

   +0 write(4, ..., 10000) = 10000
   +0 > (flowlabel 0x1) P. 1:10001(10000) ack 1 <nop,nop,TS val 100 ecr 200>
   +0 < . 1:1(0) ack 10001 win 1000 <nop,nop,TS val 200 ecr 100>
// no ECN mark, flowlabel won't change.

   +0 write(4, ..., 10000) = 10000
   +0 > (flowlabel 0x1) P. 10001:20001(10000) ack 1 <nop,nop,TS val 100 ecr 200>
   +0 < .  1:1(0) ack 17001 win 1000 <nop,nop,TS val 200 ecr 100>
   +0 < E. 1:1(0) ack 20001 win 1000 <nop,nop,TS val 200 ecr 100>
// 3/10 packets ECN-marked, >25%, 1 congested round

   +0 write(4, ..., 10000) = 10000
   +0 > (flowlabel 0x1) PW. 20001:30001(10000) ack 1 <nop,nop,TS val 100 ecr 200>
   +0 < .  1:1(0) ack 27001 win 1000 <nop,nop,TS val 200 ecr 100>
   +0 < E. 1:1(0) ack 30001 win 1000 <nop,nop,TS val 200 ecr 100>
// 3/10 packets ECN-marked, >25%, 2 consecutive congested rounds

   +0 write(4, ..., 10000) = 10000
   +0 > (flowlabel 0x1) PW. 30001:40001(10000) ack 1 <nop,nop,TS val 100 ecr 200>
   +0 < .  1:1(0) ack 37001 win 1000 <nop,nop,TS val 200 ecr 100>
   +0 < E. 1:1(0) ack 40001 win 1000 <nop,nop,TS val 200 ecr 100>
// 3/10 packets ECN-marked, >25%, 3 consecutive congested round

   +0 write(4, ..., 1000) = 1000
   +0 > (flowlabel 0x1) PW. 40001:41001(1000) ack 1 <nop,nop,TS val 100 ecr 200>
   +0 < .  1:1(0) ack 41001 win 1000 <nop,nop,TS val 200 ecr 100>
// This ACK started a new round. PLB observes that past three rounds were
// congested and no packets are in flight so it changes the flowlabel.

   +0 write(4, ..., 10000) = 10000
// As expected, packets of idle write carry new flow label
   +0 > (flowlabel 0x2) P. 41001:51001(10000) ack 1 <nop,nop,TS val 100 ecr 200>
   +0 < .  1:1(0) ack 51001 win 1000 <nop,nop,TS val 200 ecr 100>
